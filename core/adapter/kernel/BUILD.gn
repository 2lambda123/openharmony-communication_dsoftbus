# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  config("dsoftbus_adapter_kernel_interface") {
    include_dirs = [ "include" ]
  }

  common_include = [
    "$dsoftbus_root_path/core/common/include",
    "$libsec_include_path",
    "$hilog_lite_include_path",
  ]

  common_deps = [
    "$hilog_lite_deps_path",
    "$libsec_deps_path",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("softbus_adapter_kernel") {
      include_dirs = common_include
      include_dirs += [ "//kernel/liteos_m/kal/cmsis" ]
      sources = [
        "liteos_m/softbus_mem_interface.c",
        "liteos_m/softbus_os_interface.c",
      ]
      deps = common_deps
      public_configs = [ ":dsoftbus_adapter_kernel_interface" ]
    }
  } else {
    shared_library("softbus_adapter_kernel") {
      include_dirs = common_include
      sources = [
        "liteos_a/softbus_mem_interface.c",
        "liteos_a/softbus_os_interface.c",
      ]
      cflags = [ "-fPIC" ]
      deps = common_deps
      public_configs = [ ":dsoftbus_adapter_kernel_interface" ]
    }
  }
} else {
  import("//build/ohos.gni")

  config("config_adapter_kernel") {
    include_dirs = [ "$dsoftbus_root_path/core/adapter/kernel/include" ]
  }

  ohos_shared_library("softbus_adapter_kernel") {
    include_dirs = [ "$dsoftbus_root_path/core/common/include" ]
    sources = [
      "liteos_a/softbus_mem_interface.c",
      "liteos_a/softbus_os_interface.c",
    ]
    public_configs = [ ":config_adapter_kernel" ]

    public_deps = [ "//utils/native/base:utilsecurec_shared" ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
