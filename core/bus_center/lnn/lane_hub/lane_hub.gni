# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

core_lane_hub_path = "$dsoftbus_root_path/core/bus_center/lnn/lane_hub"

bus_center_hub_inc = [
  "$core_lane_hub_path/lane_manager/include",
  "$core_lane_hub_path/time_sync/include",
]
bus_center_hub_src = [
  "$core_lane_hub_path/lane_manager/src/lnn_lane_info.c",
  "$core_lane_hub_path/lane_manager/src/lnn_lane_manager.c",
  "$core_lane_hub_path/lane_manager/src/lnn_smart_communication.c",
]
bus_center_hub_deps = []
bus_center_hub_ldflags = []

if (defined(ohos_lite)) {
  if (ohos_kernel_type == "liteos_m") {
    bus_center_hub_src +=
        [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_manager_stub.c" ]
  } else {
    bus_center_hub_inc += [ "//utils/native/base/include" ]
    if (enable_time_sync == true) {
      bus_center_hub_src +=
          [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_manager.c" ]
      native_source_path = rebase_path("$dsoftbus_root_path")
      dep_file =
          "dsoftbus_enhance/core/bus_center/lnn/lane_hub/time_sync/BUILD.gn"
      enhanced = exec_script(
              "$dsoftbus_root_path/run_shell_cmd.py",
              [ "cd $native_source_path; if [ -f $dep_file ]; then echo true; else echo false; fi" ],
              "value")
      if (enhanced) {
        bus_center_hub_ldflags += [ "-ldsoftbus_time_sync_impl" ]
        bus_center_hub_deps += [ "$dsoftbus_root_path/dsoftbus_enhance/core/bus_center/lnn/lane_hub/time_sync:dsoftbus_time_sync_impl" ]
      } else {
        bus_center_hub_src +=
            [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_impl_stub.c" ]
      }
    } else {
      bus_center_hub_src +=
          [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_manager_stub.c" ]
    }
  }
} else {
  bus_center_hub_inc += [ "//utils/native/base/include" ]
  bus_center_hub_deps += [ "//utils/native/base:utils" ]
  if (enable_time_sync == true) {
    bus_center_hub_src +=
        [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_manager.c" ]
    native_source_path = rebase_path("$dsoftbus_root_path")
    dep_file =
        "dsoftbus_enhance/core/bus_center/lnn/lane_hub/time_sync/BUILD.gn"
    enhanced = exec_script(
            "$dsoftbus_root_path/run_shell_cmd.py",
            [ "cd $native_source_path; if [ -f $dep_file ]; then echo true; else echo false; fi" ],
            "value")
    if (enhanced) {
      bus_center_hub_deps += [ "$dsoftbus_root_path/dsoftbus_enhance/core/bus_center/lnn/lane_hub/time_sync:dsoftbus_time_sync_impl" ]
    } else {
      bus_center_hub_src +=
          [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_impl_stub.c" ]
    }
    bus_center_hub_inc += [
      "$dsoftbus_root_path/core/adapter/bus_center/include/",
      "$dsoftbus_root_path/core/bus_center/lnn/net_ledger/distributed_ledger/include",
    ]
  } else {
    bus_center_hub_src +=
        [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_manager_stub.c" ]
  }
}
