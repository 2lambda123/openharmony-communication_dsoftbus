# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  if (ohos_kernel_type == "liteos_m") {
    static_library("softbus_server_frame") {
      complete_static_lib = true
      # mini system not support
    }
  } else {
    source_set("softbus_server_frame") {
      include_dirs = [
        "common/include",
        "small/init/include",
      ]

      sources = [
        "common/src/softbus_server_frame.c",
        "small/init/src/bus_center_server_stub.c",
        "small/init/src/disc_server_stub.c",
        "small/init/src/softbus_server_stub.c",
        "small/init/src/trans_server_stub.c",
      ]

      cflags = [
        "-Wall",
        "-fPIC",
        "-std=c99",
      ]

      deps = [
        "$dsoftbus_root_path/adapter:softbus_adapter",
        "$dsoftbus_root_path/core/authentication:dsoftbus_auth_server",
        "$dsoftbus_root_path/core/bus_center:dsoftbus_bus_center_server",
        "$dsoftbus_root_path/core/bus_center/utils:dsoftbus_bus_center_utils",
        "$dsoftbus_root_path/core/common:softbus_utils",
        "$dsoftbus_root_path/core/common/security/permission:softbus_permission",
        "$dsoftbus_root_path/core/connection:conn_manager",
        "$dsoftbus_root_path/core/discovery:dsoftbus_disc_server",
        "$dsoftbus_root_path/core/frame/small/client_manager:client_manager",
        "$dsoftbus_root_path/core/transmission:dsoftbus_trans_session",
        "$dsoftbus_root_path/core/transmission/trans_channel/proxy:dsoftbus_trans_proxy_channel",
        "$hilog_lite_deps_path",
        "//foundation/communication/ipc_lite:liteipc_adapter",
        "//foundation/distributedschedule/samgr_lite/samgr:samgr",
      ]
    }
    executable("softbus_server") {
      sources = [ "small/init/src/softbus_server_main.c" ]
      include_dirs = [ "common/include" ]
      deps = [ ":softbus_server_frame" ]
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_prebuilt_etc("softbus_server.rc") {
    relative_install_dir = "init"
    if (use_musl) {
      source =
          "$dsoftbus_root_path/core/frame/standard/init/src/softbus_server.cfg"
    } else {
      source =
          "$dsoftbus_root_path/core/frame/standard/init/src/softbus_server.rc"
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }

  ohos_shared_library("softbus_server") {
    include_dirs = [
      "$dsoftbus_root_path/core/frame/standard/init/include",
      "$dsoftbus_root_path/core/frame/common/include",
    ]
    sources = [
      "common/src/softbus_server_frame.c",
      "standard/init/src/if_softbus_server.cpp",
      "standard/init/src/softbus_server.cpp",
      "standard/init/src/softbus_server_death_recipient.cpp",
      "standard/init/src/softbus_server_stub.cpp",
    ]
    deps = [
      ":softbus_server.rc",
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/authentication:dsoftbus_auth_server",
      "$dsoftbus_root_path/core/bus_center:dsoftbus_bus_center_server",
      "$dsoftbus_root_path/core/common:softbus_utils",
      "$dsoftbus_root_path/core/connection:conn_manager",
      "$dsoftbus_root_path/core/discovery:dsoftbus_disc_server",
      "$dsoftbus_root_path/core/frame/standard/client_manager:client_manager",
      "$dsoftbus_root_path/core/transmission:dsoftbus_trans_session",
      "//utils/native/base:utils",
    ]
    if (is_standard_system) {
      external_deps = [
        "hiviewdfx_hilog_native:libhilog",
        "ipc:ipc_core",
        "safwk:system_ability_fwk",
      ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
